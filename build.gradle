import org.gradle.api.artifacts.maven.MavenDeployment

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'

group = 'com.github.rholder'
version = '1.0.2-SNAPSHOT'
ext.packaging = 'jar'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task groovydocJar(type: Jar, dependsOn: 'groovydoc') {
    from groovydoc.destinationDir
    classifier = 'javadoc'
}

task exportJavadoc(type: Copy, dependsOn: 'groovydoc') {
    from groovydoc.destinationDir
    into 'javadoc'
}

// enable Gradle Wrapper support for bootstrapping with specific version
task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}

artifacts {
    archives jar
    archives sourcesJar
    archives groovydocJar
}

//********* artifact signing *********
if(gradle.startParameter.taskNames.contains("uploadArchives")) {
    signing {
        sign configurations.archives
    }
} else {
    task signArchives {
        // do nothing
    }
}

uploadArchives {
    repositories {
        if (version.contains("SNAPSHOT")) {
            mavenLocal()
        } else {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
    
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                  authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
    
                pom.project {
                   name 'gradle-autojar'
                   packaging 'jar'
                   description 'The gradle-autojar project is a Gradle plugin that uses Autojar, a specialized jar archive minimizer written by Bernd Eggink (http://autojar.sourceforge.net/), for building self-contained executable jars that include only those dependencies that are actually used.'
                   url 'http://rholder.github.io'
    
                   scm {
                       url 'scm:git@github.com:rholder/gradle-autojar.git'
                       connection 'scm:git@github.com:rholder/gradle-autojar.git'
                       developerConnection 'scm:git@github.com:rholder/gradle-autojar.git'
                   }
    
                   licenses {
                       license {
                           name 'The Apache Software License, Version 2.0'
                           url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                           distribution 'repo'
                       }
                   }
    
                   developers {
                       developer {
                           id 'rholder'
                           name 'Ray Holder'
                       }
                   }
               }
            }
        }
    }
}
